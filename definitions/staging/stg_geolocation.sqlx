config {
    type: "view",
    schema: "staging",
    assertions: {
      uniqueKey: ["unique_id"],
      nonNull: ["unique_id"]
    }
}

with source as (
    select * from ${ref("olist_geolocation_dataset")}
),

renamed as (
    select distinct
        ${surrogate_key.surrogate_key(
            ["geolocation_zip_code_prefix","geolocation_lat","geolocation_lng"]
        )} as unique_id,
        geolocation_lat as latitude,
        geolocation_lng as longitude,
        geolocation_city as city,
        geolocation_state as state,
        geolocation_zip_code_prefix as zip_code_prefix

    from source
),

deduped as (
    select * from renamed
    qualify row_number() over (partition by unique_id order by city) = 1
)

select * from deduped